apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "../artifacts.gradle"
apply from: "../spotless.gradle"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.jarsilio.android.scrambledeggsif"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 46
        versionName "1.6.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        standard {
            dimension "version"
            resValue "string", "app_name", "Scrambled Exif"
        }
        peace {
            dimension "version"
            resValue "string", "app_name", "Scrambled Exif ‚úå"
            applicationIdSuffix ".peace"
        }
        cheers {
            dimension "version"
            resValue "string", "app_name", "Scrambled Exif üçª"
            applicationIdSuffix ".cheers"
        }
        love {
            dimension "version"
            resValue "string", "app_name", "Scrambled Exif ‚ù§Ô∏è"
            applicationIdSuffix ".love"
        }
        dollar {
            dimension "version"
            resValue "string", "app_name", "Scrambled Exif üíµ"
            applicationIdSuffix ".dollar"
        }
        fortuneCookies {
            dimension "version"
            resValue "string", "app_name", "Scrambled Exif ü•†"
            applicationIdSuffix ".fortunecookies"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/android.arch.lifecycle_*.version'
        exclude 'META-INF/android.arch.core_runtime.version'
        exclude 'META-INF/com.android.support_*.version'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.exifinterface:exifinterface:1.2.0-beta01'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // AboutLibraries
    implementation('com.mikepenz:aboutlibraries:6.0.9@aar') {
        transitive = true
    }
    // It's actually mikepenz's AboutLibraries that needs these
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha01'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    // ACRA
    def acraVersion = '5.5.0'
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-notification:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    // Okio (modern I/O library)
    implementation 'com.squareup.okio:okio:2.4.3'

    // AndroidMediaUtil (just for image rotation actually)
    implementation 'com.github.juanitobananas:AndroidMediaUtil:v1.0-1'

    // libcommon
    implementation project(path: ':common')

    // Testing
    implementation 'androidx.preference:preference:1.1.0-alpha05'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha03'
}
repositories {
    mavenCentral()
}
